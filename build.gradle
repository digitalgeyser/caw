apply plugin: 'java'

def jarPath = "${System.properties['java.home']}/../lib/ant-javafx.jar"
def outDir = 'out/production/CellularAutomataWorkbench/'
def outJar = 'caw.jar'

ant.taskdef (
  resource: 'com/sun/javafx/tools/ant/antlib.xml',
  classpath: jarPath,
  uri: 'javafx'
)

sourceSets {
  main {
    java {
      srcDirs = ['src']
      output.classesDir = outDir
    }
    resources {
      srcDirs = ['src']
      output.resourcesDir = outDir
    }
  }
}

// Tasks

defaultTasks 'deploy'

task deploy(dependsOn: [ 'compileJava', 'processResources' ] ) {
  inputs.files(fileTree(outDir))
  outputs.file(file(outJar))

  doLast {
    ant.'javafx:jar'(destfile: outJar) {
      fileset(dir: 'out/production/CellularAutomataWorkbench')
      application(name:'CellularAutomataWorkbench', mainClass: 'com.dg.caw.ui.Main')
    }
  }
}

task run(dependsOn: 'deploy') << {
 "java -jar ${outJar}".execute()
}
